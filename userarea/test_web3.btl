<?btl
"layoutgrid" ?def invert if
  ( syntaxe : "template-rows" | "template-columns" | "column-gap" | "row-gap" | "other" content LAYOUTGRID )
  : layoutgrid
    local content
    local type
    "laystyle" ?var if
      type @ "other" = invert if
        laystyle "#parentlayout { grid-<!0!>: <!1!>; }" [ type @ content @ ] format addcontent
      else
        laystyle "#parentlayout { <!0!>; }" [ content @ ] format addcontent
      then
    then
  ;
then

"layoutarea" ?def invert if
  ( syntaxe : "area" | "other" areanumber content LAYOUTAREA )
  : layoutarea
    local content
    local areanum
    local type
    "laystyle" ?var if
      type @ "other" = invert if
        laystyle "#z<!0!> { grid-area: <!1!>; }" [ areanum @ content @ ] format addcontent
      else
        laystyle "#z<!0!> { <!1!>; }" [ areanum @ content @ ] format addcontent
      then
    then
  ;
then

"layout" ?def invert if
  ( syntaxe : zone_count LAYOUT )
  ( les zones s'appellent z0, z1, etc... )
  : layout
    local zonecount
    0 local i
    "html" ?var if forget html then 
    "head" ?var if forget head then 
    "title" ?var if forget title then 
    "body" ?var if forget body then 
    { tag : "html" , content : [ ] , attrs : { lang : "fr" } , container : "y" } var html 
    { tag : "head" , content : [ ] , attrs : { } , container : "y" } var head 
    { tag : "body" , content : [ ] , attrs : { } , container : "y" } var body 

    "parentlayout" div local container
    zonecount @ i
    do
      "z<!0!>" [ i @ ] format div "var z<!0!>" [ i @ ] format evaluate
      container @ "z<!0!>" [ i @ ] format addcontent
    loop
    body container @ addcontent

    "#parentlayout { display: grid; }" style var laystyle
    head laystyle addcontent

    html head addcontent 
    html body addcontent 
    { tag : "title" , content : [ ] , attrs : { } , container : "y" } var title 
    head title addcontent 
    cls 
  ;
then

4 layout
"template-columns" "200px repeat(4, 1fr)" layoutgrid
"template-rows" "50px 500px 50px" layoutgrid
"column-gap" "1px" layoutgrid
"row-gap" "1px" layoutgrid
"other" "background-color: indianred; padding:10px" layoutgrid

"area" 0 "1 / 1 / 2 / 6" layoutarea
"area" 1 "2 / 1 / 3 / 2" layoutarea
"area" 2 "2 / 2 / 3 / 6" layoutarea
"area" 3 "3 / 1 / 4 / 6" layoutarea
"other" 0 "background-color:white" layoutarea
"other" 1 "background-color:white" layoutarea
"other" 2 "background-color:white" layoutarea
"other" 3 "background-color:white" layoutarea

z0 "1" addcontent
z1 "2" addcontent
z2 "3" addcontent
z3 "4" addcontent
title "Ceci est une page test" addcontent
html @ .cr cr

html @ generate cls
?>